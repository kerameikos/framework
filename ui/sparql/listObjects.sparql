PREFIX rdf:	<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dcterms:	<http://purl.org/dc/terms/>
PREFIX skos:	<http://www.w3.org/2004/02/skos/core#>
PREFIX crm:	<http://www.cidoc-crm.org/cidoc-crm/>
PREFIX kid:	<https://kerameikos.org/id/>
PREFIX kon:	<https://kerameikos.org/ontology#>

SELECT DISTINCT ?object ?title ?id ?keeper ?manifest (group_concat(?static; separator = "|") as ?static_images) (group_concat(?iiif; separator = "|") as ?iiif_images) WHERE {
%STATEMENTS%
?object crm:P1_is_identified_by ?id1 ;
    crm:P1_is_identified_by ?id2 .
?id1 a crm:E33_E41_Linguistic_Appellation ;
    crm:P190_has_symbolic_content ?title .
?id2 a crm:E42_Identifier ;
    crm:P190_has_symbolic_content ?id .
OPTIONAL {?object crm:P50_has_current_keeper/skos:prefLabel ?keeper FILTER (langMatches(lang(?keeper), "en"))} 
?object crm:P138i_has_representation ?image .
OPTIONAL {?object crm:P138i_has_representation ?static .
	?static dcterms:format "image/jpeg"
}
OPTIONAL {?object crm:P138i_has_representation ?iiif .
	?iiif dcterms:conformsTo ?conformsTo
}
OPTIONAL {?object crm:P129i_is_subject_of ?manifest}
} GROUP BY ?object ?title ?id ?keeper ?manifest